buildscript {
    repositories {
        maven { url "https://arti.tw.ee/artifactory/plugins-release" }
        jcenter()
    }
    dependencies {
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.10.0'
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:2.2.5'
    }
}

plugins {
    id 'net.researchgate.release' version '2.2.2'
}


apply plugin: 'idea'
apply plugin: 'groovy'

idea {
    module {
        testSourceDirs += file('src/integTest/groovy')
        testSourceDirs += file('src/integTest/resources')
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}

repositories {
    jcenter()
    maven { url "https://arti.tw.ee/artifactory/libs-release" }
}

ext {
    artifactoryUser = "$System.env.ARTIFACTORY_USER"
    artifactoryPassword = "$System.env.ARTIFACTORY_PASSWORD"
}

apply from: 'https://arti.tw.ee/artifactory/ivy-release-local/com/transferwise/gradle-scripts/1.0.3/libUpload.gradle'

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile 'com.google.guava:guava:17.0'
    compile 'com.github.docker-java:docker-java:3.0.12'
    compile 'com.tapstream:rollbar-logback:0.1.3'

    testCompile 'junit:junit-dep:4.11'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.mockito:mockito-core:1.9.5'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

release {
    failOnUnversionedFiles = true
    tagPrefix = project.name
    preTagCommitMessage = "[ci skip] pre tag commit: "
    tagCommitMessage = "[ci skip] creating tag: "
    newVersionCommitMessage = "[ci skip] new version commit: "
    buildTasks = ['build']
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

test {
    systemProperty "java.awt.headless", "true"
    testLogging {
        events "passed", "skipped", "failed", "standardError"
        showStackTraces = true
        exceptionFormat = "full"
    }
}
